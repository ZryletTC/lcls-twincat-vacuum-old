<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.10">
  <POU Name="FB_903" Id="{43f992bb-7482-4786-b361-2c82626a8580}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_903
VAR_IN_OUT

	IG : ST_VG;

END_VAR
VAR_INPUT

	PG : ST_VG;

END_VAR
VAR_OUTPUT
END_VAR
VAR
	rMaxPressure: REAL := 5E-3;
	rMinPressure: REAL := 5E-11; (* Even though spec is 3E-10, manual states that the discharge is off only if output voltage is <0.5V. 
                                    Since this corresponds to ~3.2E-11 torr, let's be safe and call the min. 5E-11. *)

	V: LREAL;
	fbGaugeState : FB_PressureState;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* This function block is used to provide protection and automatic turn on of ion gauges,
 it also manages the turn on of the AT_VAC boolean, and checks to make sure the pressure is good *)

(* Real-value calculation *)
(* Generic expression for pressure in the MKS systems

P = 10^(V/0.6 -12)

*)

V := 10*INT_TO_LREAL(IG.iPRESS_R)/32767;

IG.rPRESS := LREAL_TO_REAL(EXPT(10,(V-11)));


(* Protection Functions *)
(* If the PG pressure is greater than the IG.PRO_SP then the gauge is disabled *)
(* If the PG pressure is less than the IG.PRO_SP then the gauge is enabled *)
(* This FB also implements some hysteresis so the gauge doesn't have rapid power cycling while near the turn on boundary *)

IF PG.rPRESS <= IG.rPRO_SP AND PG.xPRESS_OK THEN

	IG.xHV_DIS := IG.xHV_SW;

ELSIF IG.xHV_DIS THEN
	IF (IG.rPRESS > (IG.rPRO_SP + IG.rHYS_PR)) OR PG.rPRESS > (IG.rPRO_SP + IG.rHYS_PR) THEN
		IG.xHV_DIS := FALSE;
		IG.xHV_SW := FALSE;
	END_IF
ELSE
	IG.xHV_DIS := FALSE;
	IG.xHV_SW := FALSE;
END_IF

(* Pressure OK check *)
fbGaugeState.i_rV := V;
fbGaugeState.i_MaxPressure := rMaxPressure;
fbGaugeState.i_MinPressure := rMinPressure;
fbGaugeState(iq_stVG:=IG);

//Backwards compatibility
IG.xPRESS_OK := (IG.eState = Valid) OR IG.xBAKEOUT;

(* Setpoint evaluation *)
IG.xAT_VAC := (IG.xPRESS_OK AND (IG.rPRESS<IG.rVAC_SP)) OR IG.xBAKEOUT;




]]></ST>
    </Implementation>
    <LineIds Name="FB_903">
      <LineId Id="3" Count="48" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>