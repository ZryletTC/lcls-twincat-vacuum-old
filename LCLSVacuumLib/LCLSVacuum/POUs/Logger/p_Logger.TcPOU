<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4018.5">
  <POU Name="p_Logger" Id="{f0a01cf2-a6dc-4875-97c8-89cfb51e0a9d}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM p_Logger
VAR_INPUT
	i_sMsg	:	STRING;
	i_eSevr	:	E_MesgSevr;
	i_CloseSocket	:	BOOL;
	i_bReset	:	BOOL;
	i_eSubsystem	:	E_Subsystem; //In-lieu of App-name in the RFC-5424
	i_sPID	:	STRING := cNILVALUE;	//Not currently used
	i_sMSGID	:	STRING := cNILVALUE; //Not currently used
	i_sSD	:	STRING := cNILVALUE; //Not currently used
END_VAR
VAR_OUTPUT
	
END_VAR
VAR
	fbUDPSocketManager	:	FB_ConnectionlessSocket;
	udpSocket	:	T_HSOCKET;
	fbUDPSocketSend	:	FB_SocketUdpSendTo;
	sString	:	T_MaxString;
	eState	:	E_MessengerState;
	stDiag	:	ST_fbDiagnostics;
	rtReset	:	R_TRIG;
	fbBuffer : FB_StringRingBuffer;
	MesgBuffer	:	ARRAY [0..cMesgArraySize] OF T_MaxString;
	fbFormatString	:	FB_FormatString;
	wPRIVAL: WORD;
	i_wSevr: WORD;
	fbGetSystemTime	:	FB_LocalSystemTime := (bEnable := TRUE);
	sCurrentTime: STRING;
	fbGetHostName	:	FB_GetHostName := (bExecute := TRUE, sNetID := '');
	sHostName: T_MaxString;
END_VAR
VAR CONSTANT
	cMesgArraySize : INT := 50;
	cwFacility: WORD := 20; // local4
	// Syslog Version
	cwSyslogVer: T_Arg := 1;
	cNILVALUE	:	STRING := '-';
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Syslog Logger
A. Wallace 2016-9-3

This is used to pass PLC notification messeges to a syslog server.

A message buffer is added to using an action, and this POU constantly attempts to send out messages from the buffer.
*)

fbGetSystemTime(); 
IF fbGetSystemTime.bValid THEN
	sCurrentTime := SYSTEM_TIME_TO_RFC3339(fbGetSystemTime.systemTime);
ELSE
	sCurrentTime := cNILVALUE;
END_IF

fbGetHostName();
IF NOT (fbGetHostName.bBusy OR fbGetHostName.bError) THEN
	sHostName := fbGetHostName.sHostName;
ELSE
	sHostName := cNILVALUE;
END_IF

IF i_CloseSocket THEN 
	eState := CloseSocket; 
END_IF

rtReset(CLK:=i_bReset);
IF rtReset.Q THEN
	eState := CreateSocket;
END_IF

CASE eState OF
	Init:
		;
	CreateSocket:
		fbUDPSocketManager.nLocalPort := g_udpSyslog;
		fbUDPSocketManager.bEnable := TRUE;
		
		IF fbUDPSocketManager.eState = eSOCKET_CREATED THEN
			eState := Active;
		ELSIF fbUDPSocketManager.bError THEN
			eState := Error;
			fbUDPSocketManager.bError := FALSE;
		END_IF
	Active:
		IF fbUDPSocketManager.eState = eSOCKET_TRANSIENT  OR 
			fbUDPSocketManager.bError THEN
			 eState := Error; 
		END_IF
		
		// While the socket is active, go ahead and send whatever is in the buffer
		IF fbBuffer.nCount > 0 AND NOT fbUDPSocketSend.bBusy THEN
			fbBuffer.A_RemoveHead(getValue=>sString);
			fbUDPSocketSend.bExecute := TRUE; //reset in the action after busy goes true
		ELSIF fbUDPSocketSend.bError THEN
			eState := Error;
		ELSIF fbBuffer.nCount = 0 OR fbUDPSocketSend.bBusy THEN
			;
		ELSE
			eState := Error;
		END_IF
		
	CloseSocket:
		fbUDPSocketManager.bEnable R= i_CloseSocket;
		IF fbUDPSocketManager.eState = eSOCKET_CLOSED THEN
			eState := Init;
		ELSIF fbUDPSocketManager.bBusy AND NOT fbUDPSocketManager.bEnable THEN
			;
		ELSIF fbUDPSocketManager.bError THEN
			eState := Error;
		ELSE
			eState := Error;
		END_IF
	Error:
		eState := Init;
END_CASE



Manager();
SocketSendTo();]]></ST>
    </Implementation>
    <Action Name="A_ClearInputs" Id="{3fb7e5fb-968c-46cb-8840-cc28067f61d7}">
      <Implementation>
        <ST><![CDATA[//Reset inputs
i_sMsg := cNILVALUE;
i_eSevr := DEBUG;
i_sSubsystem := NILVALUE;]]></ST>
      </Implementation>
    </Action>
    <Action Name="LogMessage" Id="{45d57999-71ac-46eb-bde9-55811195ef52}">
      <Implementation>
        <ST><![CDATA[fbBuffer.putValue := '';
fbBuffer.pBuffer := ADR(MesgBuffer);
fbBuffer.cbSize := SIZEOF(MesgBuffer);

fbFormatString.sFormat := '<%4w> %d %s %s %s %s BOM%s';

wPRIVAL := cwFacility * 8 + i_eSevr;

fbFormatString.arg1 := wPRIVAL;
fbFormatString.arg2 := cwSyslogVer;
fbFormatString.arg3 := sCurrentTime;
fbFormatString.arg4 := acsSubsystems[i_eSubsystem];
fbFormatString.arg5 := i_sPID;
fbFormatString.arg6 := i_sSD;
fbFormatString.arg7 := i_sMsg;
fbFormatString(sOut=>fbBuffer.putValue);
fbBuffer.A_AddTail();

p_Logger.A_ClearInputs();]]></ST>
      </Implementation>
    </Action>
    <Action Name="Manager" Id="{7c7c0a0b-fb2c-4381-a6af-ddf8b7ceb1f0}">
      <Implementation>
        <ST><![CDATA[fbBuffer.pBuffer := ADR(MesgBuffer);
fbBuffer.cbBuffer := SIZEOF(MesgBuffer);

fbUDPSocketManager();]]></ST>
      </Implementation>
    </Action>
    <Action Name="SocketSendTo" Id="{e2ea3aca-dcd2-415e-9cd8-eea2c89c2a10}">
      <Implementation>
        <ST><![CDATA[fbUDPSocketSend.hSocket := udpSocket;
fbUDPSocketSend.sRemoteHost := g_IOCHostIP;
fbUDPSocketSend.nRemotePort := g_udpSyslog;
fbUDPSocketSend.pSrc := ADR(sString);
fbUDPSocketSend.cbLen := SIZEOF(sString);
fbUDPSocketSend();
fbUDPSocketSend.bExecute R= fbUDPSocketSend.bBusy;]]></ST>
      </Implementation>
    </Action>
  </POU>
</TcPlcObject>